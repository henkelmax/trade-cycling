plugins {
    id 'net.neoforged.gradle.userdev' version '7.0.+'
    id 'net.neoforged.gradle.mixin' version "${neogradle_mixin_version}"
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'com.modrinth.minotaur'
apply plugin: 'mod-update'
apply plugin: 'idea'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

archivesBaseName = archives_base_name
version = mod_version
group = maven_group

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version as int)

configurations {
    minecraftLibrary {}
    implementation.extendsFrom minecraftLibrary
}

runs {
    configureEach {
        modSource project.sourceSets.main
        dependencies {
            runtime configuration(project.configurations.minecraftLibrary)
        }
    }
    client {
        workingDirectory file('run')
        systemProperty 'forge.logging.console.level', 'debug'
        programArguments = ['--username', "henkelmax${new Random().nextInt(1000)}"]
        modSource project.sourceSets.main
    }
    server {
        workingDirectory file('run/server')
        systemProperty 'forge.logging.console.level', 'debug'
        programArguments '--nogui'
        modSource project.sourceSets.main
    }
}

tasks.withType(JavaCompile).matching(it -> !it.name.startsWith("neo")).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url = 'https://maven.maxhenkel.de/repository/public' }
}

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"

    compileOnly(project(path: ':common', configuration: 'namedElements')) {
        transitive false
    }
    shadow(project(path: ':common', configuration: 'namedElements')) {
        transitive false
    }

    minecraftLibrary "de.maxhenkel.configbuilder:configbuilder:${configbuilder_version}"
    shadow "de.maxhenkel.configbuilder:configbuilder:${configbuilder_version}"

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

mixin {
    config "${mod_id}.mixins.json"
}

jar {
    manifest {
        attributes(['Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_vendor,
                    'Specification-Version'   : '1',
                    'Implementation-Title'    : mod_name,
                    'Implementation-Version'  : mod_version,
                    'Implementation-Vendor'   : mod_vendor,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    filesMatching('**/*.toml') {
        expand 'mod_id': mod_id,
                'mod_version': mod_version,
                'mod_vendor': mod_vendor,
                'mod_name': mod_name,
                'neoforge_version_range': neoforge_version_range,
                'loader_version': loader_version,
                'minecraft_version': minecraft_version
    }
    from("${project(':common').projectDir}/src/main/resources") {
        exclude('*.mixins.json')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    it.options.release = java_version as int
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier = ''
    relocate 'de.maxhenkel.configbuilder', "de.maxhenkel.${mod_id}.configbuilder"
}
shadowJar.dependsOn('shadowJar')

curseforge {
    apiKey = file("${rootDir}/curseforge_api_key.txt").exists() ? file("${rootDir}/curseforge_api_key.txt").text : ''
    project {
        id = curse_id
        changelogType = 'markdown'
        changelog = file('changelog.md')
        releaseType = release_type
        addGameVersion curseforge_minecraft_version
        addGameVersion 'NeoForge'
        addGameVersion "Java ${java_version}"
        mainArtifact(jar) {
            displayName = "[NEOFORGE][${minecraft_version}] ${mod_name} ${mod_version}"
        }
    }
}
tasks.curseforge.dependsOn(shadowJar)

tasks.modrinth.configure({
    group = 'upload'
})
tasks.modrinthSyncBody.configure({
    group = 'upload'
})
modrinth {
    token = file("${rootDir}/modrinth_token.txt").exists() ? file("${rootDir}/modrinth_token.txt").text : ''
    projectId = modrinth_id
    versionNumber = "neoforge-${mod_version}"
    versionName = "[NEOFORGE][${minecraft_version}] ${mod_name} ${mod_version}"
    uploadFile = jar
    versionType = release_type.toUpperCase()
    changelog = file('changelog.md').text
    gameVersions = [minecraft_version]
    loaders = ['neoforge']
}
tasks.modrinth.dependsOn(shadowJar)

modUpdate {
    serverURL = 'https://update.maxhenkel.de/'
    modID = mod_id
    gameVersion = minecraft_version
    modLoader = 'neoforge'
    modVersion = mod_version
    changelogFile = file('changelog.md')
    releaseType = release_type
    tags = recommended == 'true' ? ['recommended'] : []
}